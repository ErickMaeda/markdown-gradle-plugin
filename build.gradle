/*
 * Copyright 2013-2015 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.8'
        classpath 'nu.studer:gradle-plugindev-plugin:1.0.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'nu.studer.plugindev'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.github.ben-manes.versions'
apply from: 'gradle/code-coverage.gradle'
apply from: 'gradle/code-quality.gradle'

repositories {
    jcenter()
    maven { url 'http://dl.bintray.com/content/aalmiray/kordamp' }
}

dependencies {
    compile gradleApi()
    compile 'com.overzealous:remark:0.9.3',
        'org.jsoup:jsoup:1.7.2',
        'org.apache.commons:commons-lang3:3.1',
        'org.pegdown:pegdown:1.2.1'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testCompile 'cglib:cglib-nodep:3.1'
}

plugindev {
    pluginId = 'org.kordamp.markdown.convert'
    pluginImplementationClass 'org.kordamp.gradle.markdown.MarkdownPlugin'
    pluginDescription 'A Gradle plugin for transforming Markdown to HTML and back again'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'markdown'
    authorId 'aalmiray'
    authorName 'Andres Almiray'
    authorEmail 'aalmiray@yahoo.com'
    projectUrl 'https://github.com/aalmiray/markdown-gradle-plugin'
    projectInceptionYear '2013'
    pomConfiguration {
        name 'markdown-gradle-plugin'
        description 'A Gradle plugin for transforming Markdown to HTML and back again'
        url 'https://github.com/aalmiray/markdown-gradle-plugin'
        inceptionYear '2013'
        licenses {
            license([:]) {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/aalmiray/markdown-gradle-plugin'
        }
        developers {
            [
                aalmiray : 'Andres Almiray',
                tednaleid: 'Ted Naleid',
            ].each { devId, devName ->
                developer {
                    id devId
                    name devName
                    roles {
                        role 'Developer'
                    }
                }
            }
        }
        contributors {
            [
                madmas: 'Markus Schlichting',
                iruiz : 'Ix-chel Ruiz'
            ].each { devId, devName ->
                contributor {
                    name devName
                    roles {
                        role 'contributor'
                    }
                }
            }
        }
    }
    done()
}

apply from: 'gradle/publish.gradle'